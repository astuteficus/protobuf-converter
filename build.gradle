import org.gradle.api.publish.maven.MavenPublication

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.google.protobuf:protobuf-gradle-plugin:0.8.5"
    }
}

group = 'com.github.BAData'
version = '1.1.5.CUSTOM-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

repositories {
    mavenCentral()
}

[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'

sourceCompatibility='1.7'
targetCompatibility='1.7'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    generatedFilesBaseDir = "$projectDir/src"
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.5.1'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
//            groupId ${group}
//            artifactId ${rootProject.name}
            groupId 'com.github.BAData'
            artifactId 'protobuf-converter'
        }
    }
    repositories {
        maven {
            url "gcs://batavia/repo/${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}"
        }
    }
}